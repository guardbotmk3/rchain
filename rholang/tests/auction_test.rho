// Tests for examples/auction.rho
// TODO: Remove duplication of contract once we have a contract registry

new auction in {
    contract auction (beneficiary, bidding_time, return_channel) = {
        new auction_start_channel, highest_bidder_channel, highest_bid_channel, pending_returns_channel, ended_channel, now_channel in {
            ended_channel!(false) |
            highest_bidder_channel!("no one") |
            highest_bid_channel!(-1) |
            auction_start_channel.injectNow() |
            pending_returns_channel!(Map()) |

            new bid, withdraw, auction_end in {

                contract bid(message_sender, amount, rtn) = {
                    now_channel.injectNow() |
                    for (now <- now_channel; highest_bidder <- highest_bidder_channel; highest_bid <- highest_bid_channel; auction_start <- auction_start_channel) {
                        match [now <= auction_start + bidding_time, amount > highest_bid] with
                            [true, true] => {
                                match [highest_bidder != 0] with
                                    [true] => {
                                        for (pending_returns <- pending_returns_channel) {
                                            pending_returns_channel!(
                                                pending_returns.insert(highest_bidder,
                                                    pending_returns.getOrElse(highest_bidder, 0) + highest_bid)
                                            )
                                        }
                                    }
                                    _ => {
                                        Nil
                                    } |
                                highest_bidder_channel!(message_sender) |
                                highest_bid_channel!(amount) |
                                auction_start_channel!(auction_start) |
                                rtn!(true)
                                // TODO: Send event equivalent
                            }
                            _ => {
                                highest_bidder_channel!(highest_bidder) |
                                highest_bid_channel!(highest_bid) |
                                auction_start_channel!(auction_start) |
                                rtn!(false)
                            }
                    }
                } |

                contract withdraw(message_sender, rtn) = {
                    for (pending_returns <- pending_returns_channel) {
                        match pending_returns.get(message_sender) > 0 with
                            true => {
                                // TODO: Implement polymorphic ! to actually transfer the amount of the native token
                                message_sender!(pending_returns.get(message_sender)) |
                                pending_returns_channel!(pending_returns.insert(message_sender, 0)) |
                                rtn!(true)
                            }
                            _ => {
                                pending_returns_channel!(pending_returns) |
                                rtn!(false)
                            }
                    }
                } |

                contract auction_end(message_sender, rtn) = {
                    now_channel.injectNow() |
                    for (now <- now_channel;  auction_start <- auction_start_channel; ended <- ended_channel; highest_bid <- highest_bid_channel) {
                        match [now >= auction_start + bidding_time, ended != true] with
                            [true, true] => {
                                ended_channel!(true) |
                                auction_start_channel!(auction_start) |
                                highest_bid_channel!(highest_bid) |
                                // TODO: Implement polymorphic ! to actually transfer the amount of the native token
                                beneficiary!(highest_bid) |
                                rtn!(true)
                            }
                            _ => {
                                ended_channel!(false) |
                                auction_start_channel!(auction_start) |
                                highest_bid_channel!(highest_bid) |
                                rtn!(false)
                            }
                    }
                } |
                return_channel!(
                    Map().insertMany("bid", bid, "withdraw", withdraw,
                        "auction_end", auction_end)
                )
            }
        }
    } |

    // auction_end fails if bidding time has passed
    new rtn, me, bidder_one, bidder_two, unused_rtn in {
        auction!(me, 9999, rtn) | // Bidding time of 9999 is an arbitrary time that this test shouldn't take
        for (auction_contract <- rtn) {
            @auction_contract.get("bid")!(bidder_one, 100, unused_rtn) |
            for (_ <- unused_rtn) { // Ensures sequential bid
                @auction_contract.get("bid")!(bidder_two, 200, unused_rtn) |
                for (_ <- unused_rtn) { // Ensures auction end happens after bids
                    @auction_contract.get("auction_end")!(me, unused_rtn) |
                    for (has_ended <- unused_rtn) {
                        match has_ended with
                        false => {
                            "Pass".display("\n")
                        }
                        _ => {
                            "Fail".display("\n")
                        }
                    }
                }
            } |
            rtn!(auction_contract)
        }
    } |

    // it ensures bidder_one receives back their bid
    new rtn, me, bidder_one, bidder_two, unused_rtn in {
        auction!(me, 0, rtn) | // Note bidding time of 0 means anyone can immediately end the auction
        for (auction_contract <- rtn) {
            @auction_contract.get("bid")!(bidder_one, 100, unused_rtn) |
            for (_ <- unused_rtn) { // Ensures sequential bid
                @auction_contract.get("bid")!(bidder_two, 200, unused_rtn) |
                for (_ <- unused_rtn) { // Ensures auction end happens after bids
                    @auction_contract.get("auction_end")!(me, unused_rtn) |
                    @auction_contract.get("withdraw")!(bidder_one, unused_rtn) |
                    for (bidder_one_balance <- bidder_one) {
                        match bidder_one_balance with
                        100 => {
                            "Pass".display("\n")
                        }
                        _ => {
                            "Fail".display("\n")
                        }
                    }
                }
            } |
            rtn!(auction_contract)
        }
    } |

    // It rejects lower bids
    new rtn, me, bidder_one, bidder_two, unused_rtn in {
        auction!(me, 0, rtn) | // Note bidding time of 0 means anyone can immediately end the auction
        for (auction_contract <- rtn) {
            @auction_contract.get("bid")!(bidder_one, 100, unused_rtn) |
            for (_ <- unused_rtn) { // Ensures sequential bid
                @auction_contract.get("bid")!(bidder_two, 50, unused_rtn) |
                for (_ <- unused_rtn) { // Ensures auction end happens after bids
                    @auction_contract.get("auction_end")!(me, unused_rtn) |
                    for (me_balance <- me) {
                        match me_balance with
                        100 => {
                            "Pass".display("\n")
                        }
                        _ => {
                            "Fail".display("\n")
                        }
                    }
                }
            } |
            rtn!(auction_contract)
        }
    }
}
